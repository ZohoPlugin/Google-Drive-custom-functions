
// Note: the custom function should have "lead_id" parameter
// getting record info from Leads(Modules) using the lead_id

recordMap = {"module":"Leads","id":lead_id};
resp = zoho.crm.invokeConnector("crm.get",recordMap);
lead_info = resp.get("response").get("data");
drive_folder_id = lead_info.getJSON("gdriveextension__Drive_Folder_ID");
drive_url = lead_info.getJSON("gdriveextension__Drive_URL");
kind = "";
xmlList = "";
if((drive_folder_id == "null" || drive_folder_id == null || drive_folder_id == "") && (drive_url == "null" || drive_url == null || drive_url == ""))
{
	xmlList = "<error><message>Please update the Google Drive Values by using the Add File to Google Drive button.</message></error>";
	return xmlList;
}
else
{
	tokenMap = Map:String();
	tokenMap.put("parent",drive_folder_id);
	tokenMap.put("maxResults",100);
	folder_items_map = zoho.crm.invokeConnector("gdriveextension.googledriveconnector.getdrivefilesv3",tokenMap);
	if(folder_items_map == null)
	{
		xmlList = "<error><message>Please authorize Google Drive to view Google Drive Files. You can authorize Google Drive from Google Drive Settings tab.</message></error>";
		return xmlList;
	}
	folder_items_status = folder_items_map.get("status_code");
	if(folder_items_status == "200")
	{
		listfolder_resp = folder_items_map.get("response");
		listfolder_items = listfolder_resp.getJSON("files");
		listfolder_list = listfolder_items.toJSONList();
		if(listfolder_list.toString() == null || listfolder_list.size() == 0)
		{
			xmlList = "<error><message>No files exists for this record.</message></error>";
		}
		else
		{
			i = 0;
			recordsXmlStr = "<record>";
			for each  item in listfolder_list
			{
				id = item.getJSON("id");
				title = item.getJSON("name");
				link = "https://drive.google.com/drive/folders/" + drive_folder_id;
				mimeType = item.getJSON("mimeType");
				if(mimeType == "application/vnd.google-apps.folder")
				{
					mimeType = "Folder";
					link = "https://drive.google.com/drive/folders/" + id;
				}
				else
				{
					mimeType = "File";
					link = "https://drive.google.com/file/d/" + id + "/view";
				}
				recordsXmlStr = recordsXmlStr + "<row no='" + i + "'>";
				recordsXmlStr = recordsXmlStr + "<FL val='Title' link='true' url='" + link + "'><![CDATA[" + title + "]]></FL>";
				recordsXmlStr = recordsXmlStr + "<FL val='Type'>" + mimeType + "</FL>";
				recordsXmlStr = recordsXmlStr + "</row>";
			}
			recordsXmlStr = recordsXmlStr + "</record>";
			return recordsXmlStr;
		}
	}
	else
	{
		xmlList = "<error><message>Unable to get files related to this record from Google Drive.</message></error>";
	}
	return xmlList;
}
